/*
@author Laurent Rochette
@version 1.0
@date May 25,2017
@description Error when column type is NVARCHAR, NCHAR or NTEXT.
*/

package com.datical.hammer.core.forecast

dialect "mvel"

import java.lang.String;
import java.util.List;
import org.apache.commons.lang.StringUtils;

import com.datical.hammer.core.rules.Response;
import com.datical.hammer.core.rules.Response.ResponseType;
import org.liquibase.xml.ns.dbchangelog.AddColumnType;
import org.liquibase.xml.ns.dbchangelog.ColumnAddColumnType;
import org.liquibase.xml.ns.dbchangelog.CreateTableType;
import org.liquibase.xml.ns.dbchangelog.ColumnType;

rule "NVARCHAR, NCHAR, NTEXT are NOT allowed in new tables."
	when
		//<column name="CHAR_COL" type="NCHAR">
		$createTable : CreateTableType( )
		$types : List( size > 0 )
							from collect(ColumnType(isForbidden(type))
								from $createTable.getColumn())
	then
		String errorMessage = drools.getRule().getName();
		insert(new Response(ResponseType.FAIL, errorMessage, drools.getRule().getName()));
	end

rule "NVARCHAR, NCHAR, NTEXT are NOT allowed in new columns."
	when
		$addColumn : AddColumnType( )
		$defaultValues : List( size > 0 )
							from collect(ColumnAddColumnType(isForbidden(type))
								from $addColumn.getColumn())
	then
		String errorMessage = drools.getRule().getName();
		insert(new Response(ResponseType.FAIL, errorMessage, drools.getRule().getName()));
	end

function boolean isForbidden(String type) {
	String[] forbiddenTypeList=new String[]{"NVARCHAR", "NCHAR", "NTEXT"};

	// check for NCHAR vs NCHAR(30) so you cannot use direct comparison 
	// as NCHAR and NCHAR(30) would be different
   	System.out.println("Type: " + type + 
		"  Boolean result: " + StringUtils.startsWithAny(StringUtils.upperCase(type), forbiddenTypeList));
	return StringUtils.startsWithAny(StringUtils.upperCase(type), forbiddenTypeList);
}
